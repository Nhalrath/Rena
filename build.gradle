import org.apache.tools.ant.filters.ReplaceTokens

plugins {
	id 'java'
	id 'application'
	id 'com.github.johnrengelman.shadow' version '6.0.0'
}

mainClassName = 'net.crimsonite.rena.RenaBot'

def versionObject = new Version(major: 0, minor: 1, revision: 0)
version versionObject.toString()

sourceCompatibility = 15
targetCompatibility = 15

repositories {
	jcenter()
}

dependencies {
	implementation 'net.dv8tion:JDA:4.2.0_222'
	
	implementation 'com.jagrosh:jda-utilities:3.0.5'

	implementation 'ch.qos.logback:logback-classic:1.2.3'
}

task filterVersion(type: Copy) {
	from 'src/main/java'
	into 'build/filtered'
	filter (ReplaceTokens, tokens:[
		version: versionObject.toString()
	])
}

jar {
    archiveBaseName = project.name
    manifest {
        attributes 'Implementation-Version': archiveVersion
        attributes 'Main-Class': mainClassName
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

shadowJar {
    archiveClassifier.set(null)
}

compileJava {
	source = 'build/filtered'
	options.encoding = 'UTF-8'
	
	dependsOn filterVersion
}

class Version {
	String major, minor, revision
	
	String toString() {
		"${major}.${minor}.${revision}"
	}
}

build.dependsOn shadowJar
